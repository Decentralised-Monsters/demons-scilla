scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
import ListUtils

library BondingCurvedDistributor

(* Error events *)
type Error =
  | CodeNotAuthorised
  | CodeNotMain
  | CodeInsufficientFunds
  | CodeNotContractOwner

let customization = Uint256 100000000000
let exponent = Uint32 2
let zero256 = Uint256 0
let zero128 = Uint128 0
let zero32 = Uint32 0
let one256 = Uint256 1
let one32 = Uint32 1
let nil_str_list = Nil {String}

let one_msg = 
  fun (msg : Message) => 
  let nil_msg = Nil {Message} in
  Cons {Message} msg nil_msg

let make_error =
  fun (result : Error) =>
    let result_code = 
      match result with
      | CodeNotAuthorised     => Int32 -1
      | CodeNotMain           => Int32 -2
      | CodeInsufficientFunds => Int32 -3
      | CodeNotContractOwner  => Int32 -4
      end
    in
    { _exception : "Error"; code : result_code }

let option_value =
  tfun 'A =>
  fun (default: 'A) =>
  fun (opt_val: Option 'A) =>
    match opt_val with
    | Some v => v
    | None => default
    end

let option_uint256 = @option_value Uint256
let option_uint128 = @option_value Uint128
let option_uint32 = @option_value Uint32
let str_append = @list_append String

let to_256 =
  fun (v: Uint32) =>
    let v32 = builtin to_uint256 v in
      option_uint256 zero256 v32

(* b = customization / exponent + 1 * s**(exponent + 1) *)
(* s - total_supply *)
let get_pool_balance =
  fun (s: Uint256) =>
    let n = builtin add exponent one32 in
    let n256 = to_256 n in
    let mn = builtin div customization n256 in
    let ts = builtin add s one256 in
    let s_pow = builtin pow ts n in
      builtin mul mn s_pow

(* p = (customization / exponent + 1 (s + k) ** exponent + 1) - b *)
(* s - total_supply *)
(* b - poolBalance *)
let get_price =
  fun (s: Uint256) =>
  fun (b: Uint256) =>
    let n = builtin add exponent one32 in
    let n256 = to_256 n in
    let mn = builtin div customization n256 in
    let k256 = to_256 one32 in
    let ts = builtin add s one256 in
    let sk = builtin add ts k256 in
    let sk_exp = builtin pow sk n in
    let value = builtin mul mn sk_exp in
    let amount = builtin sub value b in
    let some_amount = builtin to_uint128 amount in
      option_uint128 zero128 some_amount


let get_count =
  fun (a: Uint128) =>
  fun (p: Uint128) =>
    let value = builtin div a p in
    let some_count = builtin to_uint32 value in
      option_uint32 zero32 some_count
(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract BondingCurvedDistributor
(
  contract_owner: ByStr20,
  wallet: ByStr20,
  dmz: ByStr20,
  main: ByStr20 with contract
    field token_id_count: Uint256
  end
)

(* Mutable fields *)

(* Total token count *)
field reserve: Uint256 = zero256
field total: Uint256 = zero256

(* 20DMZ *)
field rewards: Uint128 = Uint128 20000000000000000000

field tokens_reserve: Map Uint256 String = Emp Uint256 String

(* Emit Errors *)
procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

procedure IsContractOwner()
  is_contract_owner = builtin eq contract_owner _sender;
  match is_contract_owner with
  | True => 
  | False =>
    err = CodeNotContractOwner;
    ThrowError err
  end
end

procedure IsMain()
  is_main_contract = builtin eq main _sender;

  match is_main_contract with
  | True =>
  | False =>
    err = CodeNotMain;
    ThrowError err
  end
end

procedure AddFunds(recipient: ByStr20, amount: Uint128)
  msg = {
    _tag: "AddFunds";
    _recipient: recipient;
    _amount: amount
  };
  msgs = one_msg msg;
  send msgs
end

procedure CallTransfer(to: ByStr20, amount: Uint128)
  msg_transfer = {
    _tag: "Transfer";
    _recipient: dmz;
    _amount: zero128;
    to: to;
    amount: amount
  };
  msgs = one_msg msg_transfer;
  send msgs
end

procedure Minting(to: ByStr20)
  total_reserve <- reserve;
  next_id = builtin sub total_reserve one256;
  some_uri <- tokens_reserve[next_id];

  match some_uri with
  | Some uri =>
    msg_to_demons = {
      _tag: "Mint";
      _recipient: main;
      _amount: Uint128 0;
      to: to;
      token_uri: uri
    };
    msgs = one_msg msg_to_demons;
    send msgs;
    new_total_reserve = builtin sub total_reserve one256;

    reserve := new_total_reserve;
    delete tokens_reserve[next_id]
  | None =>
  end
end

procedure AddReserve(uri: String)
  count_reserve <- reserve;

  new_count_reserve = builtin add count_reserve one256;

  tokens_reserve[new_count_reserve] := uri;
  reserve := new_count_reserve
end

transition AddReserveList(token_uris_list: List String)
  IsContractOwner;
  forall token_uris_list AddReserve
end

transition Buy()
  total_supply <- total;
  total_reserve <- reserve;
  amount_rewards <- rewards;

  b = get_pool_balance total_supply;
  price = get_price total_supply b;
  count = get_count _amount price;
  is_zero = builtin eq count zero32;

  match is_zero with
  | True =>
    err = CodeInsufficientFunds;
    ThrowError err
  | False =>
    accept;

    CallTransfer _sender amount_rewards;
    Minting _sender
  end
end

transition ReturnFunds()
  bal <- _balance;

  AddFunds wallet bal
end

(* callbacks *)
transition MintCallBack(recipient: ByStr20, token_id: Uint256, token_uri: String)
  new_count <-& main.token_id_count;

  total := new_count
end

transition RecipientAcceptTransfer(sender: ByStr20, recipient: ByStr20, amount: Uint128)
end

transition TransferSuccessCallBack(sender: ByStr20, recipient: ByStr20, amount: Uint128)
end
(* callbacks *)
