scilla_version 0

library NameLib

(* Error events *)
type Error =
| CodeNotOwner
| CodeMinLVL
| CodeNotTokenOwner
| CodeNotFound

let zero128 = Uint128 0
let zero32 = Uint32 0

let option_value =
  tfun 'A =>
  fun (default: 'A) =>
  fun (opt_val: Option 'A) =>
    match opt_val with
    | Some v => v
    | None => default
    end
  
let option_uint32 = @option_value Uint32

let one_msg = 
  fun (msg : Message) => 
  let nil_msg = Nil {Message} in
  Cons {Message} msg nil_msg

let make_error =
  fun (result : Error) =>
    let result_code = 
      match result with
      | CodeNotOwner      => Int32 -1
      | CodeMinLVL        => Int32 -2
      | CodeNotTokenOwner => Int32 -3
      | CodeNotFound      => Int32 -4
      end
    in
    { _exception : "Error"; code : result_code }

contract NameContract
(
  contract_owner: ByStr20,
  wallet: ByStr20,
  dmz: ByStr20,
  main: ByStr20 with contract
    field token_lvl: Map Uint256 Uint32,
    field token_owners: Map Uint256 ByStr20
  end
)

(* Mutable fields *)
field min_lvl_for_change: Uint32 = Uint32 5999
field price_for_change: Uint128 = Uint128 150000000000000000000

procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

procedure OnlyAdmin()
  is_owner = builtin eq contract_owner _sender;
  match is_owner with
  | True =>
  | False =>
    err = CodeNotOwner;
    ThrowError err
  end
end

procedure IsTokenOwner(token_id: Uint256)
  some_token_owner <-& main.token_owners[token_id];
  match some_token_owner with
  | Some addr => 
    is_token_owner = builtin eq addr _sender;
    match is_token_owner with
    | True =>
    | False =>
      err = CodeNotTokenOwner;
      ThrowError err
    end
  | None =>
    err = CodeNotFound;
    ThrowError err
  end
end

procedure CallSetName(token_id: Uint256, new_name: String)
  msg_set_name = {
    _tag: "SetName";
    _recipient: main;
    _amount: zero128;
    token_id: token_id;
    new_name: new_name
  };
  msgs = one_msg msg_set_name;
  send msgs
end

procedure CallTransferFrom(amount: Uint128, from: ByStr20, to: ByStr20)
  call_transfer_from = {
    _tag: "TransferFrom";
    _recipient: dmz;
    _amount: Uint128 0;
    from: from;
    to: to;
    amount: amount
  };
  msgs = one_msg call_transfer_from;
  send msgs
end

transition SetName(token_id: Uint256, new_name: String)
  IsTokenOwner token_id;

  min_lvl <- min_lvl_for_change;
  price <- price_for_change;
  some_lvl <-& main.token_lvl[token_id];
  lvl = option_uint32 zero32 some_lvl;
  can_change = builtin lt lvl min_lvl;

  match can_change with
  | False =>
    CallTransferFrom price _sender wallet;
    CallSetName token_id new_name
  | True =>
    err = CodeMinLVL;
    ThrowError err
  end
end

transition ChangeMinLevel(value: Uint32)
  OnlyAdmin;

  min_lvl_for_change := value;

  e = {
    _eventname: "MinLevelChanged";
    min_lvl_for_change: value
  };
  event e
end

transition ChangePrice(value: Uint128)
  OnlyAdmin;

  price_for_change := value;

  e = {
    _eventname: "PriceChanged";
    price_for_change: value
  };
  event e
end

(* callbacks *)
transition TransferFromSuccessCallBack(initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128)
end
