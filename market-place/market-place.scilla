scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
import BoolUtils IntUtils

library MarketPlace

let not_pause = Uint32 0
let on_pause = Uint32 1
let zero128 = Uint128 0
let one_hundred128 = Uint128 100
let zero_address = 0x0000000000000000000000000000000000000000

let one_msg = 
  fun (msg : Message) => 
  let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

let one = Uint256 1

let option_value =
  tfun 'A =>
  fun (default: 'A) =>
  fun (opt_val: Option 'A) =>
    match opt_val with
    | Some v => v
    | None => default
    end

let option_address = @option_value ByStr20

let uint128_to_uint256 : Uint128 -> Uint256 =
  fun (x : Uint128) =>
    let ox256 = builtin to_uint256 x in
      match ox256 with
      | None =>
        (* this never happens, hence we throw a division by zero exception just in case *)
        let zero = Uint256 0 in
        builtin div zero zero
      | Some x256 => x256
      end

(* Compute "(x * y) / z" with protection against integer overflows *)
let muldiv : Uint128 -> Uint128 -> Uint128 -> Uint128 =
    fun (x : Uint128) =>
    fun (y : Uint128) =>
    fun (z : Uint128) =>
      let x256 = uint128_to_uint256 x in
      let y256 = uint128_to_uint256 y in
      let z256 = uint128_to_uint256 z in
      let x_mul_y256 = builtin mul x256 y256 in
      let res256 = builtin div x_mul_y256 z256 in
      let ores128 = builtin to_uint128 res256 in
      match ores128 with
      | None =>
        (* this must never happen, hence we throw an integer overflow exception *)
        let max_uint128 = Uint128 340282366920938463463374607431768211455 in
        let fourtytwo128 = Uint128 42 in
        builtin mul max_uint128 fourtytwo128
      | Some res128 =>
        res128
      end

(*ByStr20 - Address of owner*)
(*Uint128 - Price*)
(*Uint256 - tokenID*)
(*Uint256 - orderID*)
type Order =
| Order of ByStr20 Uint128 Uint256 Uint256

(* Error exceptions *)
type Error =
  | CodeMPNotApprovedToTransfer
  | CodeOrderNotFound
  | CodeNotAuthorized
  | CodeNotTokenOwner
  | CodePauseNotPause
  | CodeTokenListedInAuction
  | CodeTokenAlreadyListed
  | CodeCommissionOutOfRange

let make_error =
  fun (result : Error) =>
    let result_code = 
      match result with
      | CodeMPNotApprovedToTransfer  => Int32 -1
      | CodeOrderNotFound            => Int32 -2
      | CodeNotAuthorized            => Int32 -3
      | CodeNotTokenOwner            => Int32 -4
      | CodePauseNotPause            => Int32 -5
      | CodeTokenListedInAuction     => Int32 -6
      | CodeTokenAlreadyListed       => Int32 -7
      | CodeCommissionOutOfRange     => Int32 -8
      end
    in
    { _exception : "Error"; code : result_code }

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract MarketPlace
(
  contract_owner: ByStr20,
  init_wallet: ByStr20,
  init_dmz: ByStr20,
  main: ByStr20 with contract
    field token_owners: Map Uint256 ByStr20,
    field token_approvals: Map Uint256 ByStr20
  end,
  init_auction: ByStr20 with contract
    field token_auctions: Map Uint256 Uint256
  end
)


field owner: ByStr20 = contract_owner
field pending_owner: Option ByStr20 = None {ByStr20}
field dmz: ByStr20 = init_dmz
field wallet: ByStr20 = init_wallet
field auction_listing: ByStr20 with contract
  field token_auctions: Map Uint256 Uint256
end = init_auction

field pause: Uint32 = not_pause
(*Order IDs - use global variable to track order numbers *)
field order_id : Uint256 = Uint256 0
(* Orderbook: mapping (order_id => ( Order struct )) *)
field orderbook : Map Uint256 Order
                  = Emp Uint256 Order
(* token_id to order_id mapping for quick check if card is already on sale *)
field token_orderbook : Map Uint256 Uint256 = Emp Uint256 Uint256 
(* Fee taken from each purchase, 5% *)
field commission: Uint128 = Uint128 5

(* Procedures *)
procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

procedure CheckApproved(token_id: Uint256)
  some_address <-& main.token_approvals[token_id];
  address = option_address zero_address some_address;
  is_self = builtin eq _this_address address;

  match is_self with
  | True =>
  | False =>
    err = CodeMPNotApprovedToTransfer;
    ThrowError err
  end
end

(* check commission is between 0 to 100% *)
procedure CheckCommission(new_commission: Uint128)
  is_gte_zero = uint128_ge new_commission zero128;
  is_lte_hundred = uint128_le new_commission one_hundred128;
  is_within_range = andb is_gte_zero is_lte_hundred;

  match is_within_range with
  | True =>
  | False =>
    err = CodeCommissionOutOfRange;
    ThrowError err
  end
end

procedure CallTransferFromDMZ(amount: Uint128, from: ByStr20, to: ByStr20)
  dmz_o <- dmz;
  call_transfer_from = {
    _tag: "TransferFrom";
    _recipient: dmz_o;
    _amount: zero128;
    from: from;
    to: to;
    amount: amount
  };
  msgs = one_msg call_transfer_from;
  send msgs
end

procedure TransferToOwner(token_id: Uint256, order_owner: ByStr20)
  transfered = {
    _tag: "Transfer";
    _recipient: main;
    _amount: zero128;
    to: order_owner;
    token_id: token_id
  };
  msgs = one_msg transfered;
  send msgs
end

procedure TransferToMarketPlace(token_id: Uint256)
  transfered = {
    _tag: "TransferFrom";
    _recipient: main;
    _amount: zero128;
    to: _this_address;
    token_id: token_id
  };
  msgs = one_msg transfered;
  send msgs
end

procedure IsContractOwner()
  current_owner <- owner;
  is_contract_owner = builtin eq _sender current_owner;
  match is_contract_owner with
  | True => 
  | False =>
    err = CodeNotAuthorized;
    ThrowError err 
  end
end

procedure CheckPause(expected: Uint32)
  curent_pause <- pause;
  is_eq = builtin eq expected curent_pause;
  match is_eq with
  | True =>
  | False =>
    err = CodePauseNotPause;
    ThrowError err
  end
end

procedure CancelOrder(cancel_order_id: Uint256)
  listing <- orderbook[cancel_order_id];
  match listing with
  | Some (Order order_owner price token_id item_order_id) =>
    delete orderbook[item_order_id];
    delete token_orderbook[token_id];
    e = {
      _eventname: "OrderCancelled";
      item_order_id: item_order_id
    };
    event e;
    TransferToOwner token_id order_owner
  | None =>
    err = CodeOrderNotFound;
    ThrowError err
  end
end

(* check if token_id is already in auction contract *)
procedure CheckAuctionListing(token_id: Uint256)
  current_auction_listing <- auction_listing;
  auction_id <-& current_auction_listing.token_auctions[token_id];
  
  match auction_id with
  | Some id =>
    err = CodeTokenListedInAuction;
    ThrowError err
  | None =>
  end
end


procedure CheckTokenOwner(token_id: Uint256)
  some_token_owner <-& main.token_owners[token_id];
  token_owner = option_address zero_address some_token_owner;
  is_address_call = builtin eq _sender token_owner;
  match is_address_call with
  | False =>
    err = CodeNotTokenOwner;
    ThrowError err
  | True =>
  end
end


(* @dev Creates a listing to sell a NFT *)
(* User must invoke demons contract - SetApprove(marketplace_contract, token_id) prior to this *)
(* @param token_id: The demon token_id to be put up for sale. *)
(* @param price: The price to be sold in dmz decimal places. *)
transition Sell(token_id: Uint256, price: Uint128)
  CheckAuctionListing token_id;
  CheckPause not_pause;
  CheckApproved token_id;
  CheckTokenOwner token_id;

  some_order_id <- token_orderbook[token_id];

  match some_order_id with
  | Some existing_order =>
    (* token_id is already listed on sale *)
    err = CodeTokenAlreadyListed;
    ThrowError err
  | None =>
    TransferToMarketPlace token_id;
    current_order_id <- order_id;
    new_order = Order _sender price token_id current_order_id;
    orderbook[current_order_id] := new_order;
    token_orderbook[token_id] := current_order_id;

    new_order_id = builtin add current_order_id one;
    order_id := new_order_id
  end
end

(* @dev Purchase a NFT that is on sale *)
(* @param purchase_order_id: The order_id listing which contains the NFT on sale *) 
(* User must invoke dmz contract - IncreaseAllowance(marketplace_contract, price) prior to this *)
transition Purchase(purchase_order_id: Uint256)
  CheckPause not_pause;
  listing <- orderbook[purchase_order_id];
  current_commission <- commission;
  match listing with
  | Some (Order order_owner price token_id item_order_id) =>  
      (* amount is correct go ahead and remove order and transfer Dragon plus funds*)
      (* remove fee from the purchase amount*)
      fees = muldiv price current_commission one_hundred128;
      sellers_amount = builtin sub price fees;

      wallet_o <- wallet;
      CallTransferFromDMZ sellers_amount _sender order_owner;
      CallTransferFromDMZ fees _sender wallet_o;

      (* Delete the order from the orderbook*)
      delete orderbook[item_order_id];
      delete token_orderbook[token_id];
      (* transfer to new owner*)
      TransferToOwner token_id _sender;
      e = {
        _eventname : "PurchaseSuccess";
        item_order_id: item_order_id;
        token_id: token_id;
        buyer_address: _sender;
        seller_address: order_owner;
        price_sold: sellers_amount;
        fee: fees
      };
      event e
  | None => 
    err = CodeOrderNotFound;
    ThrowError err
  end
end


(* Cancel listing, remove it, and transfer Dragon back to owner*) 
(* Only performed by owner of listing*)
transition CancelListing(cancel_order_id : Uint256)
  CheckPause not_pause;
  listing <- orderbook[cancel_order_id];
  match listing with
  | Some (Order order_owner price token_id item_order_id) =>  
    (*check if owner of order*)
    is_order_owner = builtin eq _sender order_owner;
    match is_order_owner with
    | True => 
      (*Go ahead delete the order and return Dragon*)
      delete orderbook[item_order_id];
      delete token_orderbook[token_id];
      e = {
        _eventname: "OrderCanceled";
        extracted_order: item_order_id
      };
      event e;
      (*transfer demon to owner*)
      TransferToOwner token_id order_owner
    | False =>
      err = CodeNotAuthorized;
      ThrowError err
    end
  | None => 
    err = CodeOrderNotFound;
    ThrowError err
  end
end

(* Cancel an order and refund the NFT when the contract is on pause *)
transition CancelOrders(orders_list: List Uint256)
  CheckPause on_pause;
  IsContractOwner;
  forall orders_list CancelOrder
end

(* @dev Updates the commission percentage *)
(* @param new_commission: New commision, e.g. if 10%, new_commission = 10 *)
transition UpdateCommission(new_commission: Uint128)
  IsContractOwner;
  CheckCommission new_commission;
  old_com <- commission;
  commission := new_commission;
  e = {
    _eventname: "UpdateCommission";
    previous_commission: old_com;
    new_commission: new_commission
  };
  event e
end

(* @dev Pause or unpause the marketplace; used only by contract owner *)
transition UpdatePause()
  IsContractOwner;
  status <- pause;
  is_pause = builtin eq status on_pause;
  match is_pause with
  | True =>
    pause := not_pause
  | False =>
    pause := on_pause
  end;
  updated_pause <- pause;
  e = {_eventname : "UpdatePause"; pause: updated_pause};
  event e
end

(* @dev Updates the marketplace contract address; used only by contract owner *)
transition UpdateAuctionListing(new_auction: ByStr20 with contract field token_auctions: Map Uint256 Uint256 end)
  IsContractOwner;
  auction_listing := new_auction;
  e = {_eventname : "UpdateAuctionListing"; new_auction_listing: new_auction};
  event e
end

(* @dev Updates the dmz contract address; used only by contract owner *)
transition UpdateDMZ(new_dmz: ByStr20)
  IsContractOwner;
  dmz := new_dmz;
  e = {_eventname: "UpdateDMZ"; new_dmz: new_dmz};
  event e
end

(* @dev Updates the wallet contract address; used only by contract owner *)
transition UpdateWallet(new_wallet: ByStr20)
  IsContractOwner;
  wallet := new_wallet;
  e = {_eventname: "UpdateWallet"; new_wallet: new_wallet};
  event e
end

(* @dev: Sets a new contract owner; used by contract owner only. *)
(* Current owner can abort the process by calling this transition with their own address *)
(* @param: new_owner New owner address *)
transition RequestOwnershipTransfer(new_owner: ByStr20)
  IsContractOwner;
  po = Some {ByStr20} new_owner;
  pending_owner := po;
  e = {_eventname: "RequestOwnershipTransfer"; new_owner: new_owner};
  event e
end

(* @dev: New owner accepts the ownership transfer request; used by the new owner only. *)
transition ConfirmOwnershipTransfer()
  optional_po <- pending_owner;
  match optional_po with
  | Some pend_owner =>
    caller_is_new_owner = builtin eq _sender pend_owner;
    match caller_is_new_owner with
    | True =>
      (* transfer ownership *)
      owner := pend_owner;
      none = None {ByStr20};
      pending_owner := none;
      e = {_eventname: "ConfirmOwnershipTransfer"; new_owner: pend_owner};
      event e
    | False =>
      (* caller is not the new owner, do nothing *)
      err = CodeNotAuthorized;
      ThrowError err
    end
  | None =>
    (* ownership transfer is not in-progress, do nothing *)
  end
end

(*Empty callbacks*)
(* required for marketplace CallTransferFromDMZ *)
transition TransferFromSuccessCallBack(initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128)
end